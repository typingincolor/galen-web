repositories {
    jcenter()
    flatDir {
        dirs 'libs'
    }
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.2.5.RELEASE'
        classpath 'com.bmuschko:gradle-docker-plugin:2.5'
    }
}

group = 'typingincolor'

apply plugin: 'spring-boot'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'com.bmuschko.docker-remote-api'

jar {
    baseName = 'galenweb'
    version = '0.1.0'
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.boot:spring-boot-starter-thymeleaf'
    compile 'org.slf4j:slf4j-api:1.7.12'
    compile name: 'influxdb-java-2.0-SNAPSHOT'
    compile 'com.google.guava:guava:18.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp:2.4.0'
    compile 'org.springframework.hateoas:spring-hateoas:0.17.0.RELEASE'
    compile 'org.apache.httpcomponents:httpclient:4.5'
    compile 'org.apache.httpcomponents:fluent-hc:4.5'
    compile 'com.jayway.jsonpath:json-path:2.0.0'

    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'com.github.tomakehurst:wiremock:1.56'
    testCompile 'com.google.code.findbugs:annotations:3.0.0'
}

import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

docker {
    url = 'https://192.168.59.103:2376'
    certPath = new File(System.properties['user.home'], '.boot2docker/certs/boot2docker-vm')
}

task createDockerfile(type: Dockerfile) {
    destFile = project.file('build/docker/Dockerfile')

    from 'java:8'
    maintainer 'Andrew Braithwaite "andrew@losd.info"'
    volume '/tmp'
    addFile 'galenweb-0.1.0.jar', 'app.jar'
    runCommand "bash -c 'touch /app.jar'"
    runCommand "mkdir /data"
    entryPoint 'java', '-Djava.security.egd=file:/dev/./urandom','-jar','/app.jar'
}

task buildImage(type: DockerBuildImage) {
    dependsOn createDockerfile
    inputDir = createDockerfile.destFile.parentFile
    tag = 'typingincolor/galen-web'
    doFirst {
        copy {
            from jar
            into 'build/docker'
        }
    }
}
